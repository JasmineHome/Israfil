defineTest(israfilAddLibrary) {
    LIBNAME = $$1
    isEmpty(LIBNAME):error(LIBNAME must be set)
    INCLUDEPATH += $$SOURCE_TREE/src/$$LIBNAME
    DEPENDPATH += $$SOURCE_TREE/src/$$LIBNAME
    LIBS *= -L$$BUILD_TREE/lib
    LIBS += -l$$LIBNAME
    export(INCLUDEPATH)
    export(DEPENDPATH)
    export(LIBS)
}
# Copies the given files to the destination directory
defineTest(copyToDestdir) {
    files = $$1

    for(FILE, files) {
        DDIR = $$DESTDIR

        # Replace slashes in paths with backslashes for Windows
        win32:FILE ~= s,/,\\,g
        win32:DDIR ~= s,/,\\,g

        QMAKE_POST_LINK += $$QMAKE_COPY $$quote($$FILE) $$quote($$DDIR) $$escape_expand(\\n\\t)
    }

    export(QMAKE_POST_LINK)
}
